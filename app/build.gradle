import kotlin.Pair

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'



Pair<Integer, String> getAppVersion(boolean increment = true) {
    def file = new File(projectDir, "version.properties")

    def version = new Properties()
    version.load(new FileReader(file.path))

    def major = Integer.parseInt(version.getProperty("major.version"))
    def minor = Integer.parseInt(version.getProperty("minor.version"))
    def build = Integer.parseInt(version.getProperty("build.version"))

    if (increment) {
        version.setProperty("build.version", String.valueOf(build + 1))
        version.store(new FileWriter(file.path), "build-${build + 1}")
    }

    return new Pair(build + minor * 10**3 + major * 10**5, "$major.$minor.$build")
}


android {
    compileSdkVersion 29
    defaultConfig {
        def v = getAppVersion(false)

        applicationId "uz.islom"
        minSdkVersion 16
        targetSdkVersion 29


        versionCode v.first
        versionName v.second

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

    }

    buildTypes {

        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "IslomUz-" + "${variant.name}" + "<" + "${variant.versionName}" + ">" + ".apk"
            }
        }

        debug {

            buildConfigField "String", "SERVER_URL", '"http://185.74.7.10:8091/"'

            applicationIdSuffix '.debug'

            debuggable true
            minifyEnabled false
        }

        release {

            buildConfigField "String", "SERVER_URL", '"http://185.74.7.10:8091/"'

            minifyEnabled true
            useProguard true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    splits {
        abi {
            enable gradle.startParameter.taskNames.any { it.contains("assembleRelease") }

            reset()

            include 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'

            universalApk true
        }
    }


}

kapt {
    useBuildCache = true
}


dependencies {

    def lifecycle_version = "2.1.0"
    def room_version = "2.2.1"
    def play_service_version = "17.0.0"
    def androidx_version ="1.1.0"
    def exo_player_version="2.9.4"
    def ok_http_version="3.12.1"
    def retrofit_version ="2.5.0"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.2.0-alpha01'

    implementation "androidx.appcompat:appcompat:$androidx_version"
    implementation "androidx.vectordrawable:vectordrawable:$androidx_version"
    implementation "androidx.preference:preference-ktx:$androidx_version"
    implementation "androidx.core:core-ktx:$androidx_version"

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$lifecycle_version"// For Kotlin use lifecycle-reactivestreams-ktx
    kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation "com.google.android.gms:play-services-maps:$play_service_version"
    implementation "com.google.android.gms:play-services-location:$play_service_version"

    //rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.12'

    // okHttp
    implementation "com.squareup.okhttp3:okhttp:$ok_http_version"  // do not upgrade! it is for api<21
    implementation "com.squareup.okhttp3:logging-interceptor:$ok_http_version"

    //networkManager
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // picasso
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    //exoPlayer
    implementation "com.google.android.exoplayer:exoplayer-core:$exo_player_version"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exo_player_version"
    implementation "com.google.android.exoplayer:extension-okhttp:$exo_player_version"

    //custom views
    implementation 'me.yokeyword:swipebackfragment:0.4.0'
    implementation 'me.tankery.lib:circularSeekBar:1.1.7'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'

    //log, crashlytics
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //hijri
    implementation 'joda-time:joda-time:2.10.4'


    //test
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.mockito:mockito-core:1.10.19'

}
